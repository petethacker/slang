@call "tools\buildscripts\fetch_dependencies.bat"
if errorlevel 1 ( goto ErrorFetchingDependencies )

:: list of the files that are generated by premake. unfortunately this list is manual for now but it would be good to have it automated. 
@for %%x in (
    "slang.sln"
    "source\core\core.vcxproj"
    "source\core\core.vcxproj.filters"
    "source\slang\slang.vcxproj"
    "source\slang\slang.vcxproj.filters"
    "source\slang-glslang\slang-glslang.vcxproj"
    "source\slang-glslang\slang-glslang.vcxproj.filters"
) do @if exist %%x (
    echo Deleting %%x
    del %%x 2> nul
    if errorlevel 1 ( 
        echo "Error deleting %%x"
        exit /b 1
    )
)


@echo Generating project files
@"%PM_premake_PATH%\premake5.exe" --file=premake5.lua vs2015
if errorlevel 1 ( goto ErrorRunningPremake )


:success
exit /b 0


:ErrorFetchingDependencies
echo "There was an error fetching the dependencies."
exit /b 1

:ErrorRunningPremake
echo "There was an error running premake."
exit /b 1